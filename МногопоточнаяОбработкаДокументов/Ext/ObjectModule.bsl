Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке(
		СтандартныеПодсистемыСервер.ВерсияБиблиотеки());
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительнаяОбработка();
	ПараметрыРегистрации.Версия = "1.0.0.0";
	ПараметрыРегистрации.БезопасныйРежим = Ложь;
	
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Представление = НСтр("ru = 'Многопоточная обработка документов'");
	НоваяКоманда.Идентификатор = "МногопоточнаяОбработкаДокументов";
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы();
	НоваяКоманда.ПоказыватьОповещение = Истина;
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

Процедура ВыполнитьДействиеСДокументом(СтруктураПараметров, АдресРезультата) Экспорт
	РезультатОбработки = Новый Структура("КоличествоДокументов,СписокОшибок");
	СписокОшибок = Новый Массив;
	Записано = 0;
	Итерация = 0;
	Сообщение = Новый СообщениеПользователю;
	МассивДокументов = СтруктураПараметров.Порция;
	Действие = СтруктураПараметров.Действие;
	ПараметрыСтрока = "";
	Если Действие = "УстановитьПометкуУдаления" Тогда
		ИмяМетода = "УстановитьПометкуУдаления";
		ПараметрыСтрока = "Истина";
	ИначеЕсли Действие = "СнятьПометкуУдаления" Тогда	
	    ИмяМетода = "УстановитьПометкуУдаления";
		ПараметрыСтрока = "Ложь";
	ИначеЕсли Действие = "ОтменитьПроведение" Тогда
		ИмяМетода = "Записать";
		ПараметрыСтрока = "РежимЗаписиДокумента.ОтменаПроведения";
	ИначеЕсли Действие = "Провести" Тогда
		ИмяМетода = "Записать";
		ПараметрыСтрока = "РежимЗаписиДокумента.Проведение";
	КонецЕсли;
	Для Каждого ДокументСсылка Из МассивДокументов Цикл
		Итерация = Итерация + 1;
		Сообщение.Текст = Итерация;
		Сообщение.Сообщить();
		Попытка
			Объект = ДокументСсылка.ПолучитьОбъект();
			Выполнить "Объект." + ИмяМетода + "(" + ПараметрыСтрока + ")";
		Исключение
			НоваяСтрока = Новый Структура("Документ, Ошибка", ДокументСсылка, ОписаниеОшибки());
			СписокОшибок.Добавить(НоваяСтрока);
			Продолжить;
		КонецПопытки;
		Записано = Записано + 1;
	КонецЦикла;
	
	РезультатОбработки.КоличествоДокументов = Записано;
	РезультатОбработки.СписокОшибок = СписокОшибок;
	ПоместитьВоВременноеХранилище(РезультатОбработки, АдресРезультата);
КонецПроцедуры